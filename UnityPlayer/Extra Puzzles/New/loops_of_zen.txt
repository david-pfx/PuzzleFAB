title Loops of Zen
author puzzlang
homepage puzzlang.org

background_color lightblue
text_color darkbrown

run_rules_on_level_start
hover
========
OBJECTS
========
Background .
green

edge *
lightgrey lightblue blue
012012012
120120120
201201201
012012012
120120120
201201201
012012012
120120120
201201201

focus 
grey
00000000
0......0
0......0
0......0
0......0
0......0
0......0
00000000

randomiser r
red

lit
lightbrown

done
transparent

sun
transparent

tile1 1
red
....0....
....0....
....0....
....0....
....0....
.........
.........
.........
.........

tile2 2
red
.........
.........
.........
.........
....00000
.........
.........
.........
.........

tile3 3
red
....0....
....0....
....0....
....0....
....00000
.........
.........
.........
.........

tile4 4
red
.........
.........
.........
.........
....0....
....0....
....0....
....0....
....0....

tile5 5
red
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....

tile6 6
red
.........
.........
.........
.........
....00000
....0....
....0....
....0....
....0....

tile7 7
red
....0....
....0....
....0....
....0....
....00000
....0....
....0....
....0....
....0....

tile8 8
red
.........
.........
.........
.........
000000...
.........
.........
.........
.........

tile9 9
red
....0....
....0....
....0....
....0....
00000....
.........
.........
.........
.........

tileA A
red
.........
.........
.........
.........
000000000
.........
.........
.........
.........

tileB B
red
....0....
....0....
....0....
....0....
000000000
.........
.........
.........
.........

tileC C
red
.........
.........
.........
.........
00000....
....0....
....0....
....0....
....0....

tileD D
red
....0....
....0....
....0....
....0....
00000....
....0....
....0....
....0....
....0....

tileE E
red
.........
.........
.........
.........
000000000
....0....
....0....
....0....
....0....

tileF F
red
....0....
....0....
....0....
....0....
000000000
....0....
....0....
....0....
....0....

=======
LEGEND
=======

tilehas1 = 1 or 3 or 5 or 7 or 9 or B or D or F
tilehas2 = 2 or 3 or 6 or 7 or A or B or E or F
tilehas4 = 4 or 5 or 6 or 7 or C or D or E or F
tilehas8 = 8 or 9 or A or B or C or D or E or F

clickable = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or A or B or C or D or E or F

tileI = 5 or A
tileT = 7 or E or D or B
tileL = 3 or 6 or C or 9

w = sun and 1
x = sun and 3
y = sun and 7
z = sun and f

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
edge lit randomiser
tile1 tile2 tile3 tile4 tile5 tile6 tile7 tile8 tile9 tileA tileB tileC tileD tileE tileF
focus
sun
done

======
RULES
======

[ r ][ tileI ] -> [ r ][ random tileI ]
[ r ][ tileL ] -> [ r ][ random tileL ]
[ r ][ tileT ] -> [ r ][ random tileT ]
[ r ] -> [ * ]

[ focus clickable ] -> [ clickable ]
[ hover clickable ] -> [ focus clickable ]

( left click clockwise)
[ fire1 1 ] -> [ focus 2 ]
[ fire1 2 ] -> [ focus 4 ]
[ fire1 4 ] -> [ focus 8 ]
[ fire1 8 ] -> [ focus 1 ]

[ fire1 3 ] -> [ focus 6 ]
[ fire1 6 ] -> [ focus C ]
[ fire1 C ] -> [ focus 9 ]
[ fire1 9 ] -> [ focus 3 ]

[ fire1 5 ] -> [ focus A ]
[ fire1 A ] -> [ focus 5 ]

[ fire1 B ] -> [ focus 7 ]
[ fire1 D ] -> [ focus B ]
[ fire1 E ] -> [ focus D ]
[ fire1 7 ] -> [ focus E ]

( right click anticlockwise)
[ fire2 2 ] -> [ focus 1 ]
[ fire2 4 ] -> [ focus 2 ]
[ fire2 8 ] -> [ focus 4 ]
[ fire2 1 ] -> [ focus 8 ]

[ fire2 6 ] -> [ focus 3 ]
[ fire2 C ] -> [ focus 6 ]
[ fire2 9 ] -> [ focus C ]
[ fire2 3 ] -> [ focus 9 ]

[ fire2 A ] -> [ focus 5 ]
[ fire2 5 ] -> [ focus A ]

[ fire2 7 ] -> [ focus B ]
[ fire2 B ] -> [ focus D ]
[ fire2 D ] -> [ focus E ]
[ fire2 E ] -> [ focus 7 ]

( what to light? )
[ lit ] -> [ ]
[ sun ] -> [ lit sun ]

startloop
    ( ' )
    up    [ 1 | lit tilehas4 ] -> [ lit 1 | lit tilehas4 ]
    right [ 2 | lit tilehas8 ] -> [ lit 2 | lit tilehas8 ]
    down  [ 4 | lit tilehas1 ] -> [ lit 4 | lit tilehas1 ]
    left  [ 8 | lit tilehas2 ] -> [ lit 8 | lit tilehas2 ]

    ( I )
    up    [ 5 | lit tilehas4 ] -> [ lit 5 | lit tilehas4 ]
    down  [ 5 | lit tilehas1 ] -> [ lit 5 | lit tilehas1 ]
    right [ A | lit tilehas8 ] -> [ lit A | lit tilehas8 ]
    left  [ A | lit tilehas2 ] -> [ lit A | lit tilehas2 ]

    ( L )
    up    [ 3 | lit tilehas4 ] -> [ lit 3 | lit tilehas4 ]
    right [ 3 | lit tilehas8 ] -> [ lit 3 | lit tilehas8 ]
    right [ 6 | lit tilehas8 ] -> [ lit 6 | lit tilehas8 ]
    down  [ 6 | lit tilehas1 ] -> [ lit 6 | lit tilehas1 ]
    down  [ C | lit tilehas1 ] -> [ lit C | lit tilehas1 ]
    left  [ C | lit tilehas2 ] -> [ lit C | lit tilehas2 ]
    left  [ 9 | lit tilehas2 ] -> [ lit 9 | lit tilehas2 ]
    up    [ 9 | lit tilehas4 ] -> [ lit 9 | lit tilehas4 ]

    ( T )
    up    [ 7 | lit tilehas4 ] -> [ lit 7 | lit tilehas4 ]
    right [ 7 | lit tilehas8 ] -> [ lit 7 | lit tilehas8 ]
    down  [ 7 | lit tilehas1 ] -> [ lit 7 | lit tilehas1 ]
    right [ E | lit tilehas8 ] -> [ lit E | lit tilehas8 ]
    down  [ E | lit tilehas1 ] -> [ lit E | lit tilehas1 ]
    left  [ E | lit tilehas2 ] -> [ lit E | lit tilehas2 ]
    up    [ D | lit tilehas4 ] -> [ lit D | lit tilehas4 ]
    down  [ D | lit tilehas1 ] -> [ lit D | lit tilehas1 ]
    left  [ D | lit tilehas2 ] -> [ lit D | lit tilehas2 ]
    up    [ B | lit tilehas4 ] -> [ lit B | lit tilehas4 ]
    right [ B | lit tilehas8 ] -> [ lit B | lit tilehas8 ]
    left  [ B | lit tilehas2 ] -> [ lit B | lit tilehas2 ]

    ( + )
    up    [ F | lit tilehas4 ] -> [ lit F | lit tilehas4 ]
    right [ F | lit tilehas8 ] -> [ lit F | lit tilehas8 ]
    down  [ F | lit tilehas1 ] -> [ lit F | lit tilehas1 ]
    left  [ F | lit tilehas2 ] -> [ lit F | lit tilehas2 ]
endloop

( win condition is both lit and fully connected )
[ done ] -> [ ]
[ lit clickable ] -> [ lit clickable done ]
up    [ done tilehas1 | no tilehas4 ] -> [ tilehas1 | ]
right [ done tilehas2 | no tilehas8 ] -> [ tilehas2 | ]
down  [ done tilehas4 | no tilehas1 ] -> [ tilehas4 | ]
left  [ done tilehas8 | no tilehas2 ] -> [ tilehas8 | ]

==============
WINCONDITIONS
==============

all clickable on done

=======
LEVELS
=======

r*****
******
**11**
**x3**
******
******

r****
*353*
*7x5*
*133*
*****

r*****
*41EC*
*y879*
*7Cx4*
*83A8*
******

r*********
*42C468.4*
*5.7B9.2D*
*xC5.44.5*
*6BD.7BAD*
*16BE96AD*
*.52969.5*
*29..3829*
**********
